plugins {
    id "org.jetbrains.intellij" version "0.0.43"
}

apply plugin: 'idea'
apply plugin: 'org.jetbrains.intellij'
apply plugin: 'java'

sourceCompatibility = javaVersion
targetCompatibility = javaVersion

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

intellij {
    // IntelliJ IDEA dependency
    version project.hasProperty('intellijVersion') ? project.getProperty('intellijVersion') : ideaVersion
    // Bundled plugin dependencies 
    plugins = ['maven', 'gradle', 'Groovy', 'yaml']
    pluginName 'Minecraft Development'
    updateSinceUntilBuild false

    downloadSources Boolean.valueOf(downloadIdeaSources)
    sandboxDirectory project.rootDir.canonicalPath + "/.sandbox"
}

idea {
    project {
        jdkName = 1.8
        languageLevel = 1.8
    }

    module {
        excludeDirs += file('.sandbox');
    }
}

group 'com.demonwav.minecraft-dev'
version pluginVersion

task initProp(type: Copy) {
    def baseProp = new File('src/main/resources/messages.MinecraftDevelopment_en.properties')
    def baseEnglishProp = new File('src/main/resources/messages.MinecraftDevelopment.properties')

    def comment = '''# Do not manually edit this file
# This file is automatically copied from messages.MinecraftDevelopment_en_US.properties at build time
'''

    from('src/main/resources/messages.MinecraftDevelopment_en_US.properties')
    eachFile { details ->
        details.copyTo(baseProp)
        details.copyTo(baseEnglishProp)

        baseProp.write comment + baseProp.text
        baseEnglishProp.write comment + baseEnglishProp.text
    }
    into 'src/main/java/resources/'
    outputs.upToDateWhen { false }
}

afterEvaluate {
    buildPlugin.dependsOn initProp
    runIdea.dependsOn initProp
}
